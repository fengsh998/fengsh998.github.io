---
layout: post
title: "AEM开发系列-HTL语法规范简篇(七)"
categories: AEM开发系列
tags: AEM
author: fengsh998
typora-root-url: ..
---

# 语法、语义说明：
## 一、规则说明。

```shell
expression = '${' , [exprNode] , [ , '@' , optionList] , '}' ;

optionList = option {',' , option} ;

option = id [ , '=' , optionValues] ; 

optionValues = exprNode
             | '[' , valueList , ']' ;

valueList = exprNode {',' exprNode} ;

exprNode = orBinaryOp , '?' , orBinaryOp , ws , ':' , ws , orBinaryOp
         | orBinaryOp ;

orBinaryOp = andBinaryOp {, '||', andBinaryOp};

andBinaryOp = inBinaryOp {, '&&', inBinaryOp};

inBinaryOp = comparisonOp [, 'in', comparisonOp];

comparisonOp = factor [, comparisonOperator, factor];

comparisonOperator = '<'
     | '<='
     | '=='
     | '>='
     | '>'
     | '!=' ;

factor = term
       | '!' , term ;

term = propertyAccess
     | '(' , exprNode  , ')'
     | '[', valueList, ']' ;

/* Note the 'comma rule' means zero or more whitespace characters. Used to indicate optional whitespace around terminals above */
, = {ws} ;

ws = ' '
   | '\t'
   | '\r'
   | '\n'
   | '\u000B'
   | '\u00A0';

/* Note that unlike terminals above, the field access character '.' cannot have optional whitespace around it */
propertyAccess = atom {'.' id}
               | atom {'[' , exprNode , ']'};

atom = string
     | id
     | int
     | float
     | bool ;

bool = 'true'
     | 'false' ;

id = ('a'..'z'|'A'..'Z'|'_') {'a'..'z'|'A'..'Z'|'0'..'9'|'_'|':'} ;

int = ['-']('1'..'9'){'0'..'9'}
    | '0' ;

float = ['-']('1'..'9'){'0'..'9'} '.' {'0'..'9'} [exponent]
      | ['-']'0.' ('0'..'9') {'0'..'9'} [exponent]
      | ['-']('1'..'9'){'0'..'9'} exponent ;

/* An HTL comment can contain any character sequence other than '*/-->' */
comment = '<!--/*' {-('*/-->')} '*/-->' ;

/* A string can be delimited by either double or single quotes. Within these delimiters it may contain either escape sequences or any characters other than backslash and whichever quote was used for delimiting. */
string = '"' {escSeq | -('\\' | '"')} '"'
       | '\'' {escSeq | -('\\'|'\'')} '\'' ;

exponent = ('e'|'E') ['+'|'-'] ('0'..'9'){'0'..'9'} ;

escSeq = '\\' ('b'|'t'|'n'|'f'|'r'|'\"'|'\''|'\\')
       | unicodeEsc;

unicodeEsc = '\\' 'u' hexDigit hexDigit hexDigit hexDigit ;

hexDigit = ('0'..'9'|'a'..'f'|'A'..'F') ;
```
__核心规则总结__
1. __表达式 (expression)__：所有动态内容通过 <span style="color:#FF5733;">${}</span> 封装，支持 <span style="color:#FF5733;">exprNode</span>（表达式节点）和可选的 <span style="color:#FF5733;">optionList</span>（如 @ format='uppercase'）。
2. __逻辑运算__：支持三元表达式 (<span style="color:#FF5733;">?:</span>)、逻辑或 (<span style="color:#FF5733;">||</span>)、逻辑与 (<span style="color:#FF5733;">&&</span>)、包含 (<span style="color:#FF5733;">in</span>) 和比较操作 <span style="color:#FF5733;">(<, <=, ==, >=, >, !=)</span>。
3. __属性访问__：通过点号 (<span style="color:#FF5733;">.</span>) 或方括号 (<span style="color:#FF5733;">[]</span>) 访问对象属性或数组元素，如 <span style="color:#FF5733;">${user.name} 或 ${items[0]}</span>。
4. __选项列表__：通过 <span style="color:#FF5733;">@ </span>定义选项，如<span style="color:#FF5733;"> @ format='uppercase' </span>或<span style="color:#FF5733;"> @ items=[1, 2, 3]</span>，支持复杂配置。
5. __数据类型__：支持字符串 (<span style="color:#FF5733;">string</span>)、标识符 (<span style="color:#FF5733;">id</span>)、整数 (<span style="color:#FF5733;">int</span>)、浮点数 (<span style="color:#FF5733;">float</span>)、布尔值 (<span style="color:#FF5733;">bool</span>)，以及 Unicode 转义。
6. __注释__：使用 <!--/* ... */--> 格式，便于代码维护。

__实例说明：__
```html
 // 使用 || 提供默认值 'Guest'，当 properties.userName 为空时显示默认值
${properties.userName || 'Guest'}

//条件渲染
<div class="${user.isActive ? 'active' : 'inactive'}">
  ${user.isActive ? 'Welcome back!' : 'Please log in'}
</div>

//列表渲染 结合 AEM 的 data-sly-list 和 HTL 选项 @ format='uppercase'，将数组 properties.items 渲染为大写列表项。适用于动态生成菜单、产品列表等
<ul data-sly-list.item="${properties.items}">
  <li>${item @ format='uppercase'}</li>
</ul>

// 属性访问与嵌套
<p>Name: ${user.profile.name}</p>
<p>Address: ${user.profile.address[0].street}</p>

//格式化与国际化 使用 @ format 指定日期格式，@ i18n 启用国际化支持，适用于多语言网站或动态日期显示。
${properties.date @ format='yyyy-MM-dd', i18n}

//根据条件过滤和显示数据
${properties.score > 10 && properties.status in ['active', 'pending'] ? properties.title : 'Default Title'}


// 处理转议
${"Hello \"World\"!\nWelcome to HTL" @ context='text'}

```
### 1. expression 规则
__规则定义:__

<span style="color:#FF5733;">expression = '${' , [exprNode] , [ , '@' , optionList] , '}' ;</span>

__说明:__

expression 是 HTL 的核心表达式，包含在 ${} 中，可能包含一个 exprNode（表达式节点）和可选的 @ 后跟 optionList（选项列表）。exprNode 和 optionList 都是可选的。

__示例:__

1. 简单表达式，只有 exprNode:
<div style="background-color:#fafad2;padding:10px;border-radius:5px;">
  ${title}
</div>

***说明：***

仅包含一个标识符 title 作为 exprNode，没有选项列表。

2. 带选项的表达式:
<div style="background-color:#fafad2;padding:10px;border-radius:5px;">
  ${title @ <span style="color:#FF5733;">format</span>=<span style="color:#228B22;">'uppercase'</span>}
</div>

***说明：***

title 是 exprNode，@ format='uppercase' 是 optionList。

3. 空表达式:
<div style="background-color:#fafad2;padding:10px;border-radius:5px;">
  ${}
</div>

***说明：***

exprNode 和 optionList 都省略，仅有 ${}。


